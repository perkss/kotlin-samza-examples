plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion" apply false
    id 'org.springframework.boot' version '2.4.1' apply false
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version "$kotlinVersion"
}

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
            freeCompilerArgs += '-include-runtime'
        }
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "kotlin-spring"

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {
            dependency group: 'org.apache.samza', name: 'samza-core_2.11', version: "$samzaVersion"
            dependency group: 'org.apache.samza', name: 'samza-api', version: "$samzaVersion"
            dependency group: 'org.apache.samza', name: 'samza-kafka_2.11', version: "$samzaVersion"
            dependency group: 'org.apache.samza', name: 'samza-kv_2.11', version: "$samzaVersion"
            dependency group: 'org.apache.samza', name: 'samza-kv-rocksdb_2.11', version:  "$samzaVersion"
            dependency group: 'org.apache.samza', name: 'samza-test_2.11', version:  "$samzaVersion"
        }
    }

    dependencies {
        implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        testImplementation(platform('org.junit:junit-bom:5.7.0'))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}